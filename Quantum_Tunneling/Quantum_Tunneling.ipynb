{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "dd9cb630",
   "metadata": {},
   "source": [
    "# Quantum Tunnelling"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a6bf0ffd",
   "metadata": {},
   "outputs": [],
   "source": [
    "using LinearAlgebra\n",
    "using SparseArrays\n",
    "using GLMakie\n",
    "\n",
    "#  Parameters \n",
    "ħ = 1.0        # Reduced Planck constant\n",
    "m = 1.0        # Particle mass\n",
    "x_min, x_max = -10.0, 10.0  # space domain \n",
    "N = 500                    # Number of spatial points\n",
    "x = range(x_min, x_max, length=N)\n",
    "dx = step(x)\n",
    "\n",
    "# Time parameters\n",
    "t_min, t_max = 0.0, 1    # simulation time\n",
    "dt = 0.0001\n",
    "ts = t_min:dt:t_max\n",
    "\n",
    "#  Potential Barrier \n",
    "V0 = 170.0                  # Barrier height\n",
    "barrier_width = 1\n",
    "barrier_center = 0.0\n",
    "\n",
    "function V_potential(x)\n",
    "    if abs(x - barrier_center) < barrier_width/2\n",
    "        return V0\n",
    "    else\n",
    "        return 0.0\n",
    "    end\n",
    "end\n",
    "\n",
    "V_array = [V_potential(xi) for xi in x]\n",
    "\n",
    "# Single Wave Packet Moving Left to Right \n",
    "x0 = -5.0                  # Initial position\n",
    "k0 = 18.0                  # Positive momentum (moving right)\n",
    "σ = 0.5                  # Width of the Gaussian\n",
    "\n",
    "function ψ0(x)\n",
    "    return exp(- (x - x0)^2 / (2 * σ^2)) * exp(im * k0 * x)\n",
    "end\n",
    "\n",
    "\n",
    "ψ_initial = [ψ0(xi) for xi in x]\n",
    "norm_factor = sqrt(sum(abs2.(ψ_initial)) * dx)\n",
    "ψ = ψ_initial / norm_factor\n",
    "\n",
    "\n",
    "r = im * ħ * dt / (4 * m * dx^2)\n",
    "\n",
    "\n",
    "main_diag = zeros(ComplexF64, N)\n",
    "off_diag = fill(-r, N-1)\n",
    "\n",
    "for i in 1:N\n",
    "    main_diag[i] = 1 + 2r + im * dt * V_array[i] / (2 * ħ)\n",
    "end\n",
    "\n",
    "A = spdiagm(-1 => off_diag, 0 => main_diag, 1 => off_diag)\n",
    "\n",
    "main_diag_B = zeros(ComplexF64, N)\n",
    "for i in 1:N\n",
    "    main_diag_B[i] = 1 - 2r - im * dt * V_array[i] / (2 * ħ)\n",
    "end\n",
    "B = spdiagm(-1 => -off_diag, 0 => main_diag_B, 1 => -off_diag)\n",
    "\n",
    "\n",
    "A[1, :] .= 0; A[1, 1] = 1\n",
    "A[end, :] .= 0; A[end, end] = 1\n",
    "B[1, :] .= 0; B[1, 1] = 1\n",
    "B[end, :] .= 0; B[end, end] = 1\n",
    "\n",
    "\n",
    "ψsave = zeros(ComplexF64, N, length(ts))\n",
    "ψsave[:, 1] = ψ\n",
    "\n",
    "\n",
    "A_factor = factorize(A)  \n",
    "\n",
    "for j in 2:length(ts)\n",
    "    b = B * ψ\n",
    "    ψ = A_factor \\ b\n",
    "    ψsave[:, j] = ψ\n",
    "end\n",
    "\n",
    "barrier_left = barrier_center - barrier_width/2\n",
    "barrier_right = barrier_center + barrier_width/2\n",
    "\n",
    "\n",
    "left_region = x .< barrier_left\n",
    "barrier_region = (x .>= barrier_left) .& (x .<= barrier_right)\n",
    "right_region = x .> barrier_right\n",
    "\n",
    "trans_probs = zeros(length(ts))\n",
    "refl_probs = zeros(length(ts))\n",
    "barrier_probs = zeros(length(ts))  \n",
    "\n",
    "for i in 1:length(ts)\n",
    "    prob_density = abs2.(ψsave[:, i])\n",
    "    total_prob = sum(prob_density) * dx  \n",
    "    refl_probs[i] = sum(prob_density[left_region]) * dx / total_prob\n",
    "    trans_probs[i] = sum(prob_density[right_region]) * dx / total_prob\n",
    "    barrier_probs[i] = sum(prob_density[barrier_region]) * dx / total_prob\n",
    "end\n",
    "\n",
    "# Visualization\n",
    "fig = Figure(resolution=(1000, 700))\n",
    "ax = Axis(fig[1, 1], \n",
    "    xlabel=\"Position (x)\", \n",
    "    ylabel=\"Probability Density |ψ|²\", \n",
    "    title=\"Quantum Tunneling: t = 0.0000 | Refl: 0.0% | Trans: 0.0%\"\n",
    ")\n",
    "\n",
    "\n",
    "x_obs = Observable(x)\n",
    "y_obs = Observable(abs2.(ψsave[:, 1]))\n",
    "v_obs = Observable(V_array ./ maximum(V_array) .* maximum(abs2.(ψsave)))\n",
    "\n",
    "wf_line = lines!(ax, x_obs, y_obs, color=:blue, linewidth=1.5, label=\"|ψ|²\")\n",
    "\n",
    "barrier_line = lines!(ax, x_obs, v_obs, \n",
    "    color=:red, linestyle=:dash, linewidth=2, label=\"Potential Barrier\")\n",
    "\n",
    "vlines!(ax, [barrier_left, barrier_right], \n",
    "        color=:gray, linestyle=:dot, linewidth=1)\n",
    "\n",
    "xlims!(ax, x_min, x_max)\n",
    "ylims!(ax, 0, maximum(abs2.(ψsave)) * 1.1)\n",
    "\n",
    "axislegend(ax, position=:rt)\n",
    "\n",
    "framerate = 30\n",
    "record(fig, \"quantum_tunnelling.mp4\", 1:10:length(ts); framerate=framerate) do i\n",
    "    y_obs[] = abs2.(ψsave[:, i])\n",
    "    \n",
    "    ax.title[] = \"Quantum Tunneling: t = $(round(ts[i], digits=3)) | \" *\n",
    "                 \"Refl: $(round(refl_probs[i]*100, digits=1))% | \" *\n",
    "                 \"Trans: $(round(trans_probs[i]*100, digits=1))%\" *\n",
    "                 \"Barrier: $(round(barrier_probs[i]*100, digits=1))% | \" *\n",
    "                 \"Total: $(round((refl_probs[i] + trans_probs[i] + barrier_probs[i])*100, digits=1))%\"\n",
    "end\n",
    "\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.5",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
